import org.springframework.boot.gradle.plugin.SpringBootPlugin

plugins {
    id 'java'
    id 'eclipse'
    id 'application'
    id 'net.researchgate.release' version '2.6.0'
    id 'maven-publish'
    id 'maven'
    id 'signing'
    id 'com.jfrog.bintray' version '1.8.5'

    id 'org.springframework.boot' version '2.3.5.RELEASE' apply false

    // for separating out unit and integration tests
    id 'org.unbroken-dome.test-sets' version '3.0.1'
}

compileJava.options.encoding = 'UTF-8'

group = 'com.spring.loader'
archivesBaseName = 's3-loader'

eclipse {
    classpath {
        downloadJavadoc = true
        downloadSources = true
    }
}

release {
    failOnCommitNeeded = false
    failOnPublishNeeded = true
    failOnSnapshotDependencies = true
    failOnUnversionedFiles = true
    failOnUpdateNeeded = true
    revertOnFail = true
}

afterReleaseBuild.dependsOn publish

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    configurations = ['archives']
    pkg {
        repo = 'spring-properties-loader'
        name = 'spring-s3-properties-loader'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/ericdallo/spring-s3-properties-loader.git'
    }
}

repositories {
    mavenCentral()
    mavenLocal()
}

dependencies {
//    annotationProcessor("org.springframework.boot:spring-boot-configuration-processor")

    implementation "org.springframework:spring-context:5.3.0"
    implementation "org.springframework.cloud:spring-cloud-aws-core:2.2.4.RELEASE"
    implementation "org.springframework.cloud:spring-cloud-context:2.2.5.RELEASE"

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.7.0'

    testImplementation "org.assertj:assertj-core:3.18.0"
    testImplementation "org.mockito:mockito-inline:3.6.0"
    testImplementation "org.mockito:mockito-junit-jupiter:3.6.0"
    testRuntimeOnly "org.yaml:snakeyaml:1.27"

    testImplementation enforcedPlatform(SpringBootPlugin.BOM_COORDINATES)
    testImplementation 'cloud.localstack:localstack-utils:0.2.5'
    testImplementation 'com.amazonaws:aws-java-sdk-s3:1.11.896'
    testImplementation 'org.springframework.boot:spring-boot-starter-web'
    testImplementation('org.springframework.boot:spring-boot-starter-test') {
        exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
    }
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    archiveClassifier = 'sources'
}

artifacts {
    archives jar
    archives sourcesJar
}

testSets {
    integrationTest
}

check.dependsOn integrationTest

// Make all tests use JUnit 5
tasks.withType(Test) {
    useJUnitPlatform()
}

mainClassName = ''
